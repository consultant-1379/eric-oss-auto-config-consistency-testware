buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.bmuschko:gradle-docker-plugin:8.0.0"
        classpath "org.unbroken-dome.gradle-plugins.helm:helm-plugin:1.7.0"
        classpath "org.unbroken-dome.gradle-plugins.helm:helm-releases-plugin:1.7.0"
        classpath "org.unbroken-dome.gradle-plugins.helm:helm-publish-plugin:1.7.0"
    }
}

apply plugin: org.unbrokendome.gradle.plugins.helm.HelmPlugin
apply plugin: org.unbrokendome.gradle.plugins.helm.release.HelmReleasesPlugin
apply plugin: org.unbrokendome.gradle.plugins.helm.publishing.HelmPublishPlugin

ext {
    repoPrefix = gradle.ext.repoPrefix
    imageVersion = getReleaseVersion()
    defaultDockerTag = "${repoPrefix}/${project.name}:${imageVersion}".toString()
}

helm {
    namespace = "${System.env.NAMESPACE}"
    lint {
        enabled = false
    }
    filtering {
        filePatterns = ["values.yaml", "Chart.yaml"]
        values = [
                name: rootProject.name,
                defaultDockerTag: defaultDockerTag,
                version: getReleaseVersion()
        ]
    }
    publishing {
        repositories {
            artifactory('proj-eric-oss-ci-drop-testware-helm-local') {
                url = uri("https://arm.seli.gic.ericsson.se/artifactory/proj-eric-oss-ci-drop-testware-helm-local/${project.name}")
                credentials {
                    username = "${System.env.SELI_ARTIFACTORY_REPO_USER}"
                    password = "${System.env.SELI_ARTIFACTORY_REPO_PASS}"
                }
            }
        }
    }
    charts {
        main {
            sourceDir = file('deployment/chart/main')
            chartVersion = imageVersion
        }
    }
    releases {
        // This is used if you want to deploy (helm install) your chart to your cluster
        // make sure to run a package target first to generate the chart
        'eric-oss-auto-config-consistency-testware' {
            from file("build/helm/charts/${rootProject.name}-${imageVersion}.tgz")

            // Update the values you want for this deployment (like --set on the command line)
            values = [
                    'env.APP_VERSION': '1.0.0',
                    'env.BUILD_URL': project.findProperty('buildUrl'),
                    'env.PRODUCT_VERSION': project.findProperty('productVersion'),
                    'env.GAS_URL': project.findProperty('gasUrl'),
                    'env.OPTIONS_FILE': project.findProperty('optionsFile') ?: '/resources/config/preOnboarding.options.json',
                    'env.INGRESS_SCHEMA': project.findProperty('ingressSchema'),
                    'env.INGRESS_HOST': project.findProperty('ingressHost'),
                    'env.INGRESS_LOGIN_USER': project.findProperty('ingressLoginUser'),
                    'env.INGRESS_LOGIN_PASSWORD': project.findProperty('ingressLoginPassword'),
                    'env.RESTSIM_HOST': project.findProperty('restsimHost'),
            ]
        }
    }
}